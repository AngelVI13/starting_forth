0 CONSTANT REJECT
1 CONSTANT SMALL
2 CONSTANT MEDIUM
3 CONSTANT LARGE
4 CONSTANT EXTRA-LARGE
5 CONSTANT ERROR

VARIABLE COUNTS 5 CELLS ALLOT

: RESET ( -- ) COUNTS 6 CELLS ERASE ;
: COUNTER  CELLS COUNTS + ;
: TALLY  COUNTER 1 SWAP +! ;

RESET

CREATE SIZES 18 C, 21 C, 24 C, 27 C, 30 C, 255 C,

: CATEGORY ( size -- )  6 0 DO DUP SIZES I + C@ < IF DROP I LEAVE THEN LOOP ;

: LABEL ( category -- )
    CASE
        REJECT      OF ." reject "      ENDOF
        SMALL       OF ." small "       ENDOF
        MEDIUM      OF ." medium "      ENDOF
        LARGE       OF ." large "       ENDOF
        EXTRA-LARGE OF ." extra-large " ENDOF
        ERROR       OF ." error "       ENDOF
    ENDCASE ;

: EGGSIZE   CATEGORY DUP LABEL TALLY ;

: REPORT ( -- )
    PAGE
    ." QUANTITY       SIZE " CR CR
    6 0 DO  I COUNTER @ 5 U.R 7 SPACES  I LABEL CR LOOP ;
